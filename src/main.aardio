import win.ui;
/*DSG{{*/
mainForm = win.form(text="easy-window";right=759;bottom=469;border="thin")
mainForm.add()
/*}}*/

import web.blink.portable;//生成独立EXE
import web.blink.jsFunction;//可选预定义全局JS函数
import web.blink.form;//导入miniblink组件
import web.blink.layeredWindow;
import web.json;
import fsys.dlg;
import fsys.file;
import mouse;
import win.ui.minmax;
import win.ui.shadow;
import win.ui.resizeBorder;


var mb = _ARGV['layered'] == 'true' ? web.blink.layeredWindow(mainForm) : web.blink.form(mainForm);

//导出网页js脚本可以直接调用的external对象
mb.external = {
    hitCaption = function(){
        mainForm.hitCaption();
    };
    hitmax = function(){
    	return mainForm.hitmax()
    };
    hitmin = function(){
    	mainForm.hitmin()
    };
    close = function(){
        mainForm.close();
    };
    alert = function(text, title){
        mainForm.msgbox(text, title);
    };
    openFile = function(fileType, title){
    	return fsys.dlg.open(fileType, title);
    };
    openDir = function(dir, subTitle, title){
    	return fsys.dlg.opendir(dir,null, subTitle, title);
    };
    saveFile = function(fileType, title){
    	return fsys.dlg.save(fileType, title);
    };
    readFile = function(path){
        var f = fsys.file(path);
        var text = f.readAll();
        f.close();
    	return text;
    };
    writeFile = function(path, text){
        var f = fsys.file(path,"w");
        var r = f.write(text);
        f.close();
        return r;
    };
    clipRead = function(){
    	return win.clip.read();
    };
    clipWrite = function(text){
    	return win.clip.write(text);
    };
    getMousePos = function(){
        var x,y = mouse.getPos();
    	return web.json.stringify({x=x;y=y});
    };
    getPos = function(){
        var x,y,width,height = win.getPos(mainForm.hwnd,true);
        return web.json.stringify({x=x;y=y;width=width;height=height});
    };
    setPos = function(x,y,width,height){
        win.setPos(mainForm.hwnd,x,y,width,height);
    };
    getScreen = function(){
        var width,height = win.getScreen();
        return web.json.stringify({width=width;height=height});
    };
    setIcon = function(icon){
    	mainForm.setIcon(icon);
    };
    setTitle = function(title){
    	mainForm.text = title;
    };
    setTopmost = function(state){
        if(state){
            win.setTopmost(mainForm.hwnd);
        } else {
            win.setTopmost(mainForm.hwnd, false);
        }
    };

}

//打开链接时触发该事件
mb.onNavigation = function(url,navigationType){
	if( navigationType == 0/*_WKE_NAVIGATION_TYPE_LINKCLICK*/ ){

	}
	return true;
}

//窗口图标
if(_ARGV['icon']) mainForm.setIcon(_ARGV['icon']);
//窗口标题
if(_ARGV['title']) mainForm.text = _ARGV['title'];
//窗口置顶
if(_ARGV['topmost'] == 'true') win.setTopmost(mainForm.hwnd);
//是否有最小化按钮
if(_ARGV['minbox'] == 'false') mainForm.modifyStyle(0x20000/*_WS_MINIMIZEBOX*/ , 0);
//是否有最大化按钮
if(_ARGV['maxbox'] == 'false') mainForm.modifyStyle(0x10000/*_WS_MAXIMIZEBOX*/, 0);
//是否是toolwindow
if(_ARGV['toolwindow'] == 'true'){
  mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/, 0);
  mainForm.modifyStyleEx(0, 0x80/*_WS_EX_TOOLWINDOW*/);
 }
//是否允许缩放边框
//if(_ARGV['sizebox'] == 'false') mainForm.modifyStyle(0x40000/*_WS_SIZEBOX*/, 0);
//是否有边框
if(_ARGV['border'] == 'false') {
	mainForm.modifyStyle(0x800000/*_WS_BORDER*/, 0);
	mainForm.modifyStyle(0xC00000/*_WS_CAPTION*/, 0);
}
//窗体边框是否有阴影
if(_ARGV['shadow'] == 'true'){
	win.ui.shadow(mainForm).setResizeBorder(false);
}
//是否可缩放窗体边框
if(_ARGV['resizable'] != 'false') win.ui.resizeBorder(mainForm);
//是否限制窗体缩放范围
if(_ARGV['minmax'] == 'true') win.ui.minmax(mainForm);
//是否有菜单
if(_ARGV['menu'] == 'false') mainForm.modifyStyle(0x80000/*_WS_SYSMENU*/, 0);

//窗口大小及位置
mainForm.width = _ARGV['width'] ? _ARGV['width'] : 1000;
mainForm.height = _ARGV['height'] ? _ARGV['height'] : 680;
var screen_width, screen_height = win.getScreen();
mainForm.setPos((screen_width-mainForm.width)/2, (screen_height-mainForm.height)/2);

mainForm.wndproc = function(hwnd,message,wParam,lParam){
    	select( message ) {
    		case 0x204/*_WM_RBUTTONDOWN*/{

    		}
    	}
	}
	
//加载资源
if(_ARGV['url']){
	mb.go(_ARGV['url']);
	var timeout = _ARGV['timeout'] ? tonumber(_ARGV['timeout'],10):5000;
	if(!mb.wait(null, timeout)){//等待页面打开
		mb.doScript("document.write('<h1>timeout!</h1>')");
	}
}else{
    mb.doScript("document.write('<h1>404!</h1>')");
}

mainForm.show();
win.loopMessage();
